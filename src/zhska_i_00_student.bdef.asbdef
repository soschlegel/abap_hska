managed implementation in class zbp_hska_i_00_student unique;

define behavior for ZHSKA_I_00_STUDENT alias Student
persistent table zhska_00_student
lock master
//authorization master ( instance )
//etag master <field_name>
{
  field ( readonly, numbering : managed ) StudentGUID;

  field ( readonly ) IsMatriculated;

  field ( mandatory ) StudentID;

  create;
  update;
  delete;

  action ( features : instance ) matriculateStudent result [1] $self;

  validation validateStudentID on save { create; field StudentID; }

  association _Course { create; }

  mapping for zhska_00_student
  {
    StudentGUID = student_guid;
    StudentID = student_id;
    Name = name;
    IsMatriculated = is_matriculated;
  }
}

define behavior for ZHSKA_I_00_COURSE alias Course
persistent table zhska_00_course
lock dependent by _Student
{

  field ( readonly, numbering : managed ) CourseGuid;

  update;
  delete;

  field ( readonly ) StudentGuid;

  mapping for zhska_00_course
  {
    CourseGuid = course_guid;
    Description = description;
    StudentGuid = student_guid;
    Title = title;
  }


  association _Student;

}